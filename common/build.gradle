
plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.3.21'
}

repositories {
    jcenter()
}

kotlin {
    targets {
        fromPreset(getIOSPreset(), 'ios') {
            compilations.create("Common") {
                outputKinds('FRAMEWORK')
                defaultSourceSet {
                    dependsOn compilations.main.defaultSourceSet
                }

            }
        }

        fromPreset(presets.jvm, 'jvm')
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }
            
//        iosCommon {
//            kotlin.srcDir("src/iosMain/kotlin")
//            resources.srcDir("src/iosMain/resources")
//        }
    }
}

configurations {
    compileClasspath
}

task buildFramework {
    doLast {
        if (!isCalledFromXcode()) {
            throw new Exception("Please run 'buildFramework' task with all necessary properties!")
        }

        println("from: ${kotlin.targets.ios.compilations.Common.getBinary('FRAMEWORK', getXcodeConfiguration()).parentFile}")
        println("into: ${getXcodeFrameworkLocation()}")

        File frameworkDir = kotlin.targets.ios.compilations.Common.getBinary('FRAMEWORK', getXcodeConfiguration())

        copy {
            from frameworkDir.parentFile
            into getXcodeFrameworkLocation()
            include "${frameworkDir.name}/**"
            include "${frameworkDir.name}.dSYM/**"
        }
    }
}

afterEvaluate {
    if (isCalledFromXcode()) {
        buildFramework.dependsOn kotlin.targets.ios.compilations.Common.linkTaskName('FRAMEWORK', getXcodeConfiguration())
    }
}

private boolean isCalledFromXcode() {
    project.hasProperty('xcode.configuration') && project.hasProperty('xcode.framework.location')
}

private String getXcodeConfiguration() {
    project.properties['xcode.configuration'] as String
}

private String getXcodeFrameworkLocation() {
    project.properties['xcode.framework.location'] as String
}

private def getIOSPreset() {
    String name = project.hasProperty('xcode.prese') ? project.properties['xcode.prese'] : 'iosX64'
    project.kotlin.presets[name]
}
